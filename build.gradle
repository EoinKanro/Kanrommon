import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
    // see https://fabricmc.net/develop/ for new versions
    id 'fabric-loom' version '1.7-SNAPSHOT' apply false
    // see https://projects.neoforged.net/neoforged/moddevgradle for new versions
    id 'net.neoforged.moddev' version '0.1.110' apply false
}

subprojects {
    apply plugin: 'java'

    dependencies {
        compileOnly "org.projectlombok:lombok:${lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${lombok_version}"
    }

    if (name == "common") return

    tasks.register("copyIcon") {
        doLast {
            File iconFile = rootProject.file("icon.png")
            if (!iconFile.exists()) {
                throw new GradleException("icon.png not found in root project!")
            }

            File resourcesDir = file("src/main/resources")
            if (!resourcesDir.exists()) {
                resourcesDir.mkdirs()
            }

            File targetFile = new File(resourcesDir, "icon.png")
            Files.copy(iconFile.toPath(), targetFile.toPath(), StandardCopyOption.REPLACE_EXISTING)

            println "Copied icon.png to ${targetFile}"
        }
    }

    tasks.matching { it.name == 'assemble' }.configureEach {
        dependsOn(tasks.named("copyIcon"))
    }
}